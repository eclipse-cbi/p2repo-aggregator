/*
* Test syntax of build unit and related concerns.
* 
*/
import java.util.List; // cheat, need a type, don't have the actual types yet...
import java.util.Map; // - " -
import org.eclipse.b3.beelang.tests.repo.TestRepo;

function main(List argv) {
	testBuildUnitSyntax();
}
function testBuildUnitSyntax() {
	true; // test is ok if this file can be parsed without errors
}

/**
* Test the mix of statements (function, unit and builder contexts).
*/
concern TestingBuildUnitConcernSyntaxSimple {
	// define functions intermixed with other things
	function foo() : "foo";
	
	// implements with type reference
	unit-context {
		select : implements == List ;
		modify :
		 { /* what to do with units that implements List*/ };
	}
	
	function bar() : "bar";
	
	// all builders having an exact name.
	builder-context {
		select : builder ~= { name == someBuilder;};
		modify : { 
			/* what to do with builder called 'someBuilder' */ 
		};
	}
	
	function fee() : "fee"; // 
}
/**
* Test all options regarding selection of units
*/ 
concern TestingUnitContextSyntax {
	
	//--requires
	// ns, name (no version)
	unit-context {
		select : requires ~= capability { name-space == a.b ; name == c.d;};
		modify : {
		/* what to do with units that requires the capability */ 
		};
	}

	// any ns, any name
	unit-context {
		select: requires ~= capability { name-space ~= _; name ~= _; }; 
		modify: { };
	}
	
	// regexp namespace and name (check for osgi.*, and org.myorg.*
	unit-context { 
		select: requires ~= capability { 
			name-space ~= ~/osgi\..*/; 
			name ~= ~/org\.myorg\..*/; 
			version ~= 1.0.0; 
		};
		modify : { };
		}
	
	// ns, name, version range in short form
	unit-context {
		select: requires ~= capability { name-space == a.b; name == c.d; version ~= 1.0.0; };
	 	modify : { }; 
	 }

	// ns, name, version range in full form
	unit-context { 
		select: requires ~= capability { name-space == a.b; name == c.d; version ~= [1.0.0, 2.0.0]; }; 
		modify: { }; 
	}
	
	//--provides
	unit-context {
		select : provides ~= capability { name-space == a.b; name == c.d; version ~= 1.0.0.qualifier; };
		modify : { };
	}
	
	//--unit name
	unit-context { select : unit ~= { name ~= _; }; modify:{ };}
	unit-context { select : unit ~= { name ~= _; version ~= 1.0.0;}; modify : { }; }
	unit-context { select : unit ~= { name ~= ~/org\.myorg\..*/;}; modify: { };}
	unit-context { select : unit ~= { name == "my unit with exactly this funny name & stuff";}; modify: { }; }
	
	// logical expressions selecting the unit
	unit-context { 
	select: 
			unit ~= { name ~= _; } 
		&& 	implements == List 
		&& 	implements == Map 
		&& 	requires ~= capability { name-space == a; name == b; version ~= [0.0.0, 9.9.9]; }
		|| 	! unit ~= { name ~= ~/org\.myorg\.fixed/; };
	modify: { }; 
	}
}
concern TestingBuilderContextSyntax {
	// addresses builder using any input in same unit
	builder-context { select: input ~= { builder ~= _; }; modify : {}; }
	
	// same as above, but explicit 'unit'
	builder-context { select: input ~= { unit builder ~= _; }; modify : {}; }
	
	// addresses builder with a complex name in same unit
	builder-context { select: input ~= { unit builder == a.^builder.called.wanda; }; modify : {}; }

	// selecting a builder that provides a capability	
	builder-context { select: provides ~= capability { name-space == a; name == b;}; modify : {}; }
	
	// selecting a builder that provides a capability of a particular version
	builder-context { select: provides ~= capability { name-space == a; name == c;  version ~= 2.0.0.someQualifier;}; modify : {};}
	
	// selecting a builder with any name
	builder-context { select: builder ~= { name ~= _; }; modify : {}; }
	
	builder-context { select: builder ~= { name == "a.builder.with.a.qualified.type"; }; modify : {}; }
	builder-context { select: input ~= { unit builder == foo; }; modify: {}; }
	
	// builders that have a unit with a name matching a regexp, calling builders matching a regexp.
	builder-context { select: input ~= { unit ~= { name ~= ~/my\.unit\..*/; } builder ~= ~/make.*/; }; modify: {};}
	
	// regexp for ns and name capability reference
	builder-context { 
		select: input ~= { capability { 
			name-space ~= ~/osgi\..*/; name ~= ~/my\.unit\..*/; version ~= 1.0.0; } builder ~= ~/make.*/; }; 
		modify:{}; }
}
concern TestingNestedBuilderContextSyntax {
	unit-context {
		select :  unit ~= { name ~= _; };
	 	modify: { 
			/* what to do to units (in this case all untis as name ~= _ */
			builder-context { 
				select: input ~= { builder == foo; }; 
				modify: { /* what to do with builders named foo */ }; 
			}
			builder-context { 
				select: input ~= { builder == bar; }; 
				modify: { /* what to do with builders named bar */ };
			}
			/** 
			* Adding a builder called "fee" to the matched units.
			*/
			builder fee {
				input : [ unit.somebuilder() ];
				output : [ basePath [somePath/foo, anotherPath/bar] ];		
			}
		};
	}
}

concern TestingBuilderContextBody {
	builder-context {
		select: builder ~= { name ~= _;};
		modify: {
			input : {
				// adds a dependency to a/b/1.0.0 and calls its foo builder with arguments
				+ when ($this.property.is.set) 
					capability { 
						name-space : a; 
						name :"b"; 
						version : 1.0.0; }.foo("hello", "goodbye") ;
		
				// removes the builder's input of a/b calling builder named c
				- input ~= { capability { name-space == a; name == b; } builder == c; };
				
				// removes all calls to a builder called x in any of the builder's inputs
				- input ~= { capability {name-space ~= _; name ~= _; } builder == x; };		
			};
			source : {
				- source ~= a/b ;
				+ x/y [a,b,c] ;
				- annotations  ~= [$a.b, $a.c]; // TODO
				+ annotations  : { $a = 1; $b = 2; } // TODO
			};
			output : {
				+ "http://somewhere.com/a/base%20Path" [ subpathA, subpathB/c];
				- output ~= ~/\/a\/b.*/;
				- output ~= /a/basePath [subpathX];	
				- annotations ~= [$a.b, $a.c]; // TODO
				+ annotations : { $a = 1; $b = 2; } // TODO
			}; 
		};
	}
}
concern TestingUnitContextBody {
	unit-context { 
		select: unit ~={ name ~= _; }; 
		modify: {
			// TODO - group these rather than having them as :
			requires : {
			+ capability { name-space : foo;  name : "bar"; version : 1.0.0; };
			+ capability { name-space : a;  name : "b"; }; // NO {} 
			+ capability { 
				name-space : something; 
				name : "green" ;
				version : [1.0.0,2.0.0]; 
				when : $a.b.c == "green";
				} as greenStuff ; 
			- requires ~= capability { name-space ~= _; name == "mipspelled"; };
			};
			// can find the "greenStuff" declared above
			builder buildGreen { input : [ greenStuff.files() ]; }
		};
	}
}
	repository cvs TestRepo {
			connection : "http://abc.org" ; 
			local : "file://aLocalURL/"; 
			user : "hello";
			password : "hush-hush";
		}

// Define a unit
unit aTestUnit version 1.0.0 {
	provides : [ 
		capability { name-space: food.nutrion.vitamin; name : "B6"; },
		capability { name-space: food.nutrion.vitamin; name : "C"; },
		capability { name-space: food.nutrion.minarl; name : "pottassium"; } 
		];
		
	source : "resource:/a.b.c/src/";
	output : "output:/a.b.c/";
	
	repository cvs TestRepo {
			connection : "http://abc.org" ; 
			local : "file://aLocalURL/"; 
			user : "hello";
			password : "hush-hush";
		}

	resolution : [
		select-switch request.^name 
			case "a" : 	repository TestRepo { path : "foo"; }
			case _ : 	repository TestRepo
			endswitch
	];
	
	builder peel {
		output : [ bananapeel.txt ];
	}
	builder seeds {
		output : [ bananaseeds.txt ] ;
	}
	builder pulp {
		output : [ bananapulp.txt ] ;	
	}
	
}
