use java.util.List;
use java.lang.Iterable;
use org.eclipse.b3.beelang.tests.buildunittypes.Fruit;
use org.eclipse.b3.build.BuildUnit;
use org.eclipse.b3.build.BuildSet;
use org.eclipse.b3.build.PathVector;

«Define the property zesty with a default value (overrideable from command line) »
default-properties { ${zesty} = false ; }

« Invoked from IDE or command line »
function main(List argv) {
	// make a fruit sallad
	testBuildingFruitSallad();
	// make an advised fruit sallad
	with ZestRemoved {
		testBuildingFruitSallad();
	};
}

« Makes a fruitsallad and prints output »
function testBuildingFruitSallad() {
«
The fruitsallad consists of :
«{	b3.resolveAndRunBuilder("fruitsallad","make").pathIterator.collect(x |
«   URI : «{ x }»«{'\n'}»»)
}»
».print();	
}

concern ZestRemoved {
	builder-context { 
		select-builders : 
			input ~= { capability { name-space == gastronomy.taste; name == "zestyPeel";} 
			called-builder ~= _ ;
			};
		modify-selected : { 
			input : { 
				- input ~= { capability { name-space == gastronomy.taste; name == "zestyPeel"; } 
				called-builder ~= _; } ;
			};
		};
	}
}
unit fruitsallad version 1.0.0 {
	builder make {
		input : [ 
			unit { name : "apple";  version : 1.0.0; }.pulp(),
			unit { name : "banana"; version : 1.0.0; }.pulp(),
			unit { name : "orange"; version : 1.0.0; }.pulp(),
			capability { name-space : gastronomy.taste; name : "zestyPeel"; }.peel()
		];
	}
}
/**
* A Fruit with the simplest form of builders (source).
*/
unit apple version 1.0.0  is Fruit {
	source : "resource:/org.eclipse.b3.beelang.tests/src-b3/testunits/apple/";
	builder peel {
		source : applepeel.txt;
	}
	builder seeds {
		source : appleseeds.txt;
	}
	builder pulp {
		source : applepulp.txt;
	}
}

/**
* A Fruit with the simplest form of builders (source).
* Has some extra provided capabilities.
*/
unit banana version 1.0.0 is Fruit {
	source : "resource:/org.eclipse.b3.beelang.tests/src-b3/testunits/banana/";
	
	provides : [ 
		capability { name : "B6"; name-space: food.nutrition.vitamin; },
		capability { name-space: food.nutrition.vitamin; name : "C"; },
		capability { name-space: food.nutrition.minerals; name : "potassium"; }
	]; 
	
	builder peel {
		source : bananapeel.txt; 
	}
	builder seeds {
		source : bananaseeds.txt; 
	}
	builder pulp {
		source : bananapulp.txt;
	}
}

/**
* A Fruit with the simplest form of builders (source).
* Has some extra provided capabilities, and a capability provided by a builder.
*/
unit orange version 1.0.0 is Fruit {
	source : "resource:/org.eclipse.b3.beelang.tests/src-b3/testunits/orange/";
	
	provides : capability { name-space : food.nutrition.vitamin; name : "C"; };
	
	builder peel {
		provides : capability { name-space : gastronomy.taste; name : "zestyPeel"; } ;
		source : orangepeel.txt;
	}
	builder seeds {
		source : orangeseeds.txt;
	}
	builder pulp {
		source : orangepulp.txt; 
	}
}