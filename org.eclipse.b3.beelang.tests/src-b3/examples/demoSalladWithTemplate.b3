use org.eclipse.b3.beelang.tests.buildunittypes.Fruit;

« Default call from the IDE goes here »
function main(List argv) : buildFruitSallad();

« Performs the build of the fruitsallad and returns the set of files as strings »
function buildFruitSallad() :
«
The fruitsallad consists of :
«{	b3.resolveAndRunBuilder("fruitsallad","make").pathIterator.collect(x |
«   URI : «{ x }»«{'\n'}»»)
}»
».print();

« Definition of the fruitsallad, and it's input »
unit fruitsallad version 1.0.0 {
	builder make {
		input : [ 
			unit { name : "apple";  version : 1.0.0; }.pulp(),
			unit { name : "banana"; version : 1.0.0; }.pulp(),
			unit { name : "orange"; version : 1.0.0; }.pulp(),
			capability { name-space : gastronomy.taste; name : "zestyPeel"; }.peel()
		];
	}
}

« A Fruit with the simplest form of builders (source). »
unit apple version 1.0.0  is Fruit {
	source : "resource:/org.eclipse.b3.beelang.tests/src-b3/testunits/apple/";
	builder peel {
		source : applepeel.txt;
	}
	builder seeds {
		source : appleseeds.txt;
	}
	builder pulp {
		source : applepulp.txt;
	}
}

« A Fruit with some additional capabilities »
unit banana version 1.0.0 is Fruit {
	source : "resource:/org.eclipse.b3.beelang.tests/src-b3/testunits/banana/";
	
	provides : [ 
		capability { name-space: food.nutrition.vitamin; name : "B6"; },
		capability { name-space: food.nutrition.vitamin; name : "C"; },
		capability { name-space: food.nutrition.minerals; name : "potassium"; }
	]; 
	
	builder peel {
		source : bananapeel.txt; 
	}
	builder seeds {
		source : bananaseeds.txt; 
	}
	builder pulp {
		source : bananapulp.txt;
	}
}

unit orange version 1.0.0 is Fruit {
	source : "resource:/org.eclipse.b3.beelang.tests/src-b3/testunits/orange/";
	
	provides : capability { name-space : food.nutrition.vitamin; name : "C"; };
	
	« This builder provides a capability »
	builder peel {
		provides : capability { name-space : gastronomy.taste; name : "zestyPeel"; } ;
		source : orangepeel.txt;
	}
	builder seeds {
		source : orangeseeds.txt;
	}
	builder pulp {
		source : orangepulp.txt; 
	}
}